Actor StarcraftHydralisk:LowTierHumanoid 20048
{

	Health 90
	Radius 20
	Height 56
	meleerange 50
	mass 200
	MissileHeight 32
	DropItem "SmallHealth"
	DropItem "clip"
	DropItem "clip"
	Obituary "%o was killed by a hydralisk"
	PainChance 50
	Speed 10
	Species "Schydralisk"	
	MinMissileChance 1
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound "Hidralisk/AllertS"
	DeathSound "Hidralisk/Death"
	scale 0.7

States
{
	Spawn:
	HYDI A 0
	HYDI A 0 A_JumpIfInventory("Spawn2Token",1,"Spawn2")
	HYDI A 0 A_giveInventory("Spawn2Token",1)
	ZIGI A 0 A_JumpIfInventory("IsBurrowed",1,"WaitBurrow")
	HYDI A 0 ACS_NamedExecute("TakeHealTokenOnDamagedHP",0)
	Goto Spawn2
	
	Spawn2:
	HYDI ABC  8 A_Look	
	Loop	

	See:
	HYDM ABC 3 Fast A_Chase("Melee","Shoot")
	HYDM A 0 A_Jump(10,"PrepareBurrow") 
	Loop

	Melee:
	HYDS DE 6 Fast A_FaceTarget
    HYDS FG 6 Fast A_CustomMeleeAttack(25, "Hidralisk/Melee","","PredatorHit") 
    HYDS FE 6
	goto See
	
	Shoot:
	HYDS A 2 A_PlaySound("Hidralisk/ShootS",0)
	HYDS A 2 A_FaceTarget
	HYDS B 4 A_FaceTarget
	HYDS C 4 A_CustomMissile("SCHydraSpike",50,CallACS("ChoseMissleHorizontal",-10,10,Random(1,2)),0)
	Goto See
	
	Pain:
	ZIGT C 0 A_JumpIfInventory("IsBurrowed",1,"Unburrow")
	goto See

	Pain.PlayerHook:
	ZIGI A 0
	ZIGI A 64
	Goto See

	PrepareBurrow:
	ZIGI AA 0 A_CheckSight("Burrow")
	Goto See

	Unburrow:
	HYDT C 0 
	HYDT C 0 A_ChangeFlag(solid,true)
	HYDT E 4 A_PlaySound("Zerg/UnBurrow",5)
	HYDT F 4 A_ChangeFlag(FORCEPAIN,false)
	HYDT C 0 A_TakeInventory("IsBurrowed",1)
	HYDT GHI 4
	Goto See
	
	Burrow:
	HYDT C 0 
	HYDT C 0 A_ChangeFlag(solid,false)
	HYDT A 4 A_ChangeFlag(FORCEPAIN,true)
	HYDT A 4 A_PlaySound("Zerg/Burrow",5)
	HYDT C 0 A_GiveInventory("IsBurrowed",1)
	HYDT ABCD 4
	goto WaitBurrow

	WaitBurrow:
	HYDT E 8 A_LookEx(LOF_NOSOUNDCHECK|LOF_NOSEESOUND,1,256,0,360,"Unburrow")
	HYDT A 0 A_Jump(1,"Unburrow") 
	loop

	Death:
	HYDD A 0
	HYDD B 0 A_giveInventory("NeedHealingToken",1)
	HYDD A 0 A_SCREAM
	HYDD IJKL 4 
	HYDD M 8 A_NoBlocking
	HYDD M -1 
	stop
	
	Death.PlayerFlame:
	Death.FirebatFlame:
	HYDD A 0
	HYDD B 0 A_giveInventory("NeedHealingToken",1)
	HYDD A 0 A_SCREAM
	HYDD STUVWXYZ 4 
	HYDD Z 8 A_NoBlocking
	HYDD Z -1 
	stop
	
	Death.PlayerMarauderRocketShot:
	Death.Extreme:
	Death.FriendlyMine:
	Death.MOD_ROCKET:
	HYDD A 0
	HYDD B 0 A_giveInventory("NeedHealingToken",1)
	HYDD A 0 A_SCREAM
	HYDD NOP 8 
	HYDD Q 8 A_NoBlocking
	HYDD R -1 
	stop
	
}

}

Actor SCHydraSpike : SCVultureRocket
{
	damage (30)
	Speed 15
	FastSpeed 25
	DamageType SCHydraSpike
	SeeSound ""
	DeathSound "Hidralisk/ShootE"
	scale 0.3
	States
	{
	Spawn:
		HYDP A 1 Bright 
		MISL A 0 Bright A_CustomMissile("RocketTrail",Random(4,6),Random(-2,2))
		Loop
	Death:
		Stop
	}
}


