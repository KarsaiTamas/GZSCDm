Actor StarcraftZergling:LowTierHumanoid 20046
{

	Health 35
	Radius 16
	Height 30
	meleerange 50
	mass 200
	MissileHeight 32
	DropItem "SmallHealth"
	Obituary "%o was killed by a Zergling"
	PainChance 50
	Speed 25
	fastspeed 40
	Species "Sczergling"	
	MinMissileChance 1
	+MISSILEMORE
	+MISSILEEVENMORE
	SeeSound "ZLing/AllertS"
	DeathSound "ZLing/Death"
	MaxTargetRange 50

States
{
	Spawn:
	ZIGI A 0
	ZIGI A 0 A_JumpIfInventory("Spawn2Token",1,"Spawn2")
	ZIGI A 0 A_JumpIfInventory("IsBurrowed",1,"WaitBurrow")
	ZIGI A 0 A_giveInventory("Spawn2Token",1)
	ZIGI A 0 ACS_NamedExecute("TakeHealTokenOnDamagedHP",0)
	Goto Spawn2
	
	Spawn2:
	ZIGI ABC  8 A_Look	
	Loop	

	See:
	ZIGM ABCD 3 Fast A_Chase
	ZIGM A 0 A_Jump(10,"PrepareBurrow") 
	Loop

	Melee:
	ZIGS AB 2 Fast A_FaceTarget
    ZIGS C 2 Fast A_CustomMeleeAttack(15, "ZLing/ShootE","","PredatorHit") 
    ZIGS DBA 2
	goto See
	
	Pain:
	ZIGT C 0 A_JumpIfInventory("IsBurrowed",1,"Unburrow")
	goto See

	Pain.PlayerHook:
	ZIGI A 0
	ZIGI A 64
	Goto See

	PrepareBurrow:
	ZIGI AA 0 A_CheckSight("Burrow")
	Goto See

	Unburrow:
	ZIGT C 0 
	ZIGT C 0 A_ChangeFlag(solid,true)
	ZIGT E 4 A_PlaySound("Zerg/UnBurrow",5)
	ZIGT A 4 A_ChangeFlag(FORCEPAIN,false)
	ZIGT C 0 A_TakeInventory("IsBurrowed",1)
	ZIGT EFG 4
	Goto See
	
	Burrow:
	ZIGT C 0 
	ZIGT C 0 A_ChangeFlag(solid,false)
	ZIGT A 4 A_ChangeFlag(FORCEPAIN,true)
	ZIGT A 4 A_PlaySound("Zerg/Burrow",5)
	ZIGT C 0 A_GiveInventory("IsBurrowed",1)
	ZIGT ABC 4
	goto WaitBurrow

	WaitBurrow:
	ZIGT D 8 A_LookEx(LOF_NOSOUNDCHECK|LOF_NOSEESOUND,1,256,0,360,"Unburrow")
	ZIGM A 0 A_Jump(1,"Unburrow") 
	loop

	Death:
	ZIGD A 0
	ZIGD B 0 A_giveInventory("NeedHealingToken",1)
	ZIGD A 0 A_SCREAM
	ZIGD ABCD 4 
	ZIGD D 8 A_NoBlocking
	ZIGD D -1 
	stop
	
	Death.PlayerFlame:
	Death.FirebatFlame:
	ZIGD A 0
	ZIGD B 0 A_giveInventory("NeedHealingToken",1)
	ZIGD A 0 A_SCREAM
	ZIGD EFGHI 6 
	ZIGD I 8 A_NoBlocking
	ZIGD I -1 
	stop
	
	Death.PlayerMarauderRocketShot:
	Death.Extreme:
	Death.FriendlyMine:
	Death.MOD_ROCKET:
	ZIGD A 0
	ZIGD B 0 A_giveInventory("NeedHealingToken",1)
	ZIGD A 0 A_SCREAM
	ZIGD NOP 8 
	ZIGD P 8 A_NoBlocking
	ZIGD P -1 
	stop
	
}

}
