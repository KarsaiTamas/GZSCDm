class StarcraftBanshee:MidTierMech// 20020
{
	Default
	{
	Health 325;
	Radius 31;
	Height 56;
	meleerange 100;
	scale 1.5;
	DropItem "MediumHealth";
	DamageFactor "SCBanshee",0;
	mass 400;
	+FLOAT;
	+NOGRAVITY;
	MissileHeight 32;
	Obituary "%o was killed by a Banshee";
	PainChance 10;
	Speed 10;
	Species "Scbanshee"	;
	MinMissileChance 50;
	SeeSound "Banshee/Allerts";
	DeathSound "Banshee/Death";
	}
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		canRegenEnergy=true;
		SetFly(true);
		curEnergy=30;
		maxEnergy=200;
	}
	void ChangeStealth(bool visibility)
	{
		if(visibility)
		{
			if(curEnergy>100)
			{
				A_SetTranslucent(0.2,1);
				A_playsound("SCHide/CloakOn");
				canRegenEnergy=false;
				isInvisible=true;
			}
		}
		else
		{
			A_playsound("SCHide/CloakOff");
			A_SetTranslucent(1,0);
			canRegenEnergy=true;
			isInvisible=false;
		}
	}
	override void Tick()
	{
		super.Tick();
		if(isInvisible)
		{
			curEnergy-=0.1;
			if(curEnergy<=0)
			{
				ChangeStealth(false);
			}
		}
		
	}	
	

States
{
	Spawn:
	BSEI ABC  8 A_Look;
	loop;

	See:
	BSEM ABC 4 A_Chase("Shoot","Shoot");
	BSEM A 0 
	{
		if(isInvisible)
		{
			if(!CheckSight(target))
			{
				ChangeStealth(false);
			}
		}
		else
		{
			if(CheckSight(target))
			{
				ChangeStealth(true);
			}
		}
	}
	Loop;


	Pain:
	BSEM A 0;
	BSEM ABC 4 A_Wander;
	goto See;
	
	Pain.PlayerHook:
	BSEI A 0;
	BSEI A 64;
	Goto See;
	
	Shoot:
	BSEI A 4 A_FaceTarget;
	BSEI A 4 A_PlaySound("Banshee/ShootS",5);
	BSEI B 2 Bright A_CustomMissile("SCBanshee",5,20);
	BSEI B 2 A_PlaySound("Banshee/ShootS",6) ;
	BSEI B 2 Bright A_CustomMissile("SCBanshee",5,-20);
	BSEI C 16;
	Goto See;
	
	Death:
    BSED A 0 A_SCREAM;
    BSED B 0 A_SetTranslucent(1,0);
    BSED AABBCC 4 A_CustomMissile("DeathExplosions",random(40,60),Random(-20,20));
    BSED C 8 A_NoBlocking ;
    stop;
}
}


class SCBanshee : rocket
{
	Default
	{
		DamageFunction 30;
		ExplosionDamage 10;
		ExplosionRadius 64;
		Speed 15;
		FastSpeed 25;
		DamageType "SCBanshee";
		SeeSound "";
		deathsound "Banshee/ShootE";
		scale 0.3;
	}
	States
	{
	Spawn:
		BSEP A 1 Bright;
		MISL A 0 Bright A_CustomMissile("RocketTrail",Random(4,6),Random(-2,2));
		Loop;
	Death:
		MISL B 8 Bright A_Explode;
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}