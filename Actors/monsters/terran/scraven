Actor StarcraftRaven:MidTierMech 20027
{

	Health 400
	Radius 31
	Height 56
	meleerange 1200
	scale 1.5
	mass 400
	+FLOAT
	+NOGRAVITY
	MissileHeight 32
	DropItem "MediumHealth"
	Obituary "%o was killed by a Raven"
	PainChance 10
	Speed 10
	Species "scraven"	
	MinMissileChance 50
	SeeSound "Raven/Allert"
	DeathSound "Banshee/Death"
	
States
{
	Spawn:
	RAVM A 0
	RAVM A 0 A_JumpIfInventory("Spawn2Token",1,"Spawn2")
	RAVM A 0 A_giveInventory("Spawn2Token",1)
	RAVI A 0 A_giveInventory("EnergyTokenGhost",50)
	RAVI A 0 ACS_NamedExecute("TakeHealTokenOnDamagedHP",0)
	RAVI A 0 ACS_NamedExecute("EnergyGain",0)
	goto Spawn2	
	
	Spawn2:
	RAVM A 0
	RAVI AB 2 A_Look	
	loop
	
	See:
	RAVM AB 4 A_Wander
	RAVM A 0 A_jump(100,"SummonChance")
	RAVM A 0 A_jump(100,"SummonChance2")
	Loop

	SummonChance:
	RAVI A 0 A_CheckSight("See")
	RAVI A 0 A_CheckRange(500,"See")
	RAVI A 0 A_JumpIfInventory("EnergyTokenGhost",50,"SummonAutoTurret")
	goto See
	
	SummonChance2:
	RAVI A 0 A_CheckSight("See")
	RAVI A 0 A_JumpIfInventory("EnergyTokenGhost",25,"SummonSeekerMiss")
	goto See
	
	SummonSeekerMiss:
	RAVI A 0
	RAVI A 0 A_PlaySound("Raven/ShootM",5)
	RAVI A 2 A_FaceTarget
	RAVI A 8 A_TakeInventory("EnergyTokenGhost",25)
	RAVS A 8 Bright A_CustomMissile("SeekerMissileDrop",5,-20)
	RAVI BABABA 8
	Goto See
	
	SummonAutoTurret:
	RAVI A 0
	RAVI A 0 A_PlaySound("Raven/ShootT",5)
	RAVI A 0 A_PlaySound("Raven/ShootS",6)
	RAVI A 8 A_TakeInventory("EnergyTokenGhost",50)
	RAVS A 8 Bright A_CustomMissile("AutoTurretDrop",5,-20)
	RAVI B 8
	goto See
	
	Pain.PlayerHook:
	RAVI A 0
	RAVI A 64
	Goto See
	
	
	Death:
    RAVD A 0 A_SCREAM
	SCMI B 0 A_giveInventory("NeedHealingToken",1)
    RAVD B 0 A_SetTranslucent(1,0)
    RAVD AABBCC 4 A_CustomMissile("DeathExplosions",random(40,60),Random(-20,20))
    RAVD C 8 A_NoBlocking 
    stop
}
}

Actor AutoTurretDrop:grenade
{
	speed 10
	damage 0
	-BOUNCEONFLOORS
	+BOUNCEONACTORS
	deathsound "AutoTurret/Land"
	states
	{
	spawn:
	TUBB A 1
	loop
	
	death:
	TUBB A 0 A_SpawnItem("StarcraftAutoturret")
	stop
	}
}

Actor SeekerMissileDrop:rocket
{
	+BOUNCEONACTORS
	speed 10
	damage 40
	seesound ""
	states
	{
	spawn:
	SEKM A 10
	goto death
	
	death:
	TUBB A 0 A_SpawnItem("StarcraftSeekerMissile")
	stop
	}
}

