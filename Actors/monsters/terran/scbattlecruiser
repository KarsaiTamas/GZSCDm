Actor StarcraftBattlecruiser:HighTierMech 20034
{

	Health 3000
	Radius 100
	Height 100
	meleerange 100
	scale 2
	DropItem "BigHealth"
	DropItem "YamatoCannon",120
	
	DamageFactor "SCYamatocannon",0
	DamageFactor "SCCruiserLaser",0
	mass 1000
	+FLOAT
	+NOGRAVITY
	MissileHeight 32
	Obituary "%o was killed by a Battlecruiser"
	PainChance 2
	Speed 10
	Species "ScbattleCruiser"	
	MinMissileChance 50
	SeeSound "BattleCru/Allert"
	DeathSound "SCThor/Death"
	+BOSS
	+BOSSDEATH
	
States
{
	Spawn:
	BATI A 0
	BATI A 0 A_JumpIfInventory("Spawn2Token",1,"Spawn2")
	BATI A 0 A_giveInventory("Spawn2Token",1)
	BATI A 0 A_giveInventory("EnergyTokenGhost",35)
	BATI A 0 ACS_NamedExecute("TakeHealTokenOnDamagedHP",0)
	BATI A 0 ACS_NamedExecute("EnergyGain",0)
	goto Spawn2	
	
	Spawn2:
	BATI A 0
	BATI AAA  8 A_Look	
	loop
	
	See:
	BATI A 0
	BATI A 0 A_jump(50,"YamatoCheck")
	BATI AAA 4 A_Chase("Shoot","Shoot")
	Loop


	Pain:
	BATI A 4 A_Wander
	goto See
	
	Shoot:
	BATI A 0
	BATI A 0 A_FaceTarget
	BATI A 4 A_PlaySound("BattleCru/ShootS1",5)
	BATI A 4 Bright A_CustomMissile("SCCruiserLaser",85, 85)
	BATI A 4 A_PlaySound("BattleCru/ShootS1",6) 
	BATI A 4 Bright A_CustomMissile("SCCruiserLaser",85,-85)
	BATI A 4 A_PlaySound("BattleCru/ShootS1",5)
	BATI A 4 Bright A_CustomMissile("SCCruiserLaser",30, 85)
	BATI A 4 A_PlaySound("BattleCru/ShootS1",6) 
	BATI A 4 Bright A_CustomMissile("SCCruiserLaser",30,-85)
	BATI A 0 A_MonsterRefire(50, "See")
	Goto Shoot	
	
	YamatoCheck:
	BATI A 0
	BATI A 0 A_CheckSight("See")
	BATI A 0 A_JumpIfInventory("EnergyTokenGhost",100,"StartYamato")
	goto Shoot

	StartYamato:
	BATI A 0 
	BATI A 0 A_ChangeFlag(NOPAIN,true)
	BATS ABC 6
	BATS D 4 A_PlaySound("BattleCru/ShootS2",5,1.0,false,ATTN_NONE ) 
	BATS DCDCDCDCDCDCD 6 A_FaceTarget
	BATS C 6 A_PlaySound("BattleCru/ShootS3",6,1.0,false,ATTN_NONE ) 
	BATS E 4 Bright A_CustomMissile("SCYamatocannon",20,0)
	BATS DE 6 A_TakeInventory("EnergyTokenGhost",100)
	BATS CBA 6
	BATI A 0 A_ChangeFlag(NOPAIN,false)
	goto Shoot
	
	Death:
    BATD A 0 A_SCREAM
	SCMI B 0 A_giveInventory("NeedHealingToken",1)
    BATD B 0 A_SetTranslucent(1,0)
    BATD AABBCC 4 A_CustomMissile("DeathExplosions",random(40,60),Random(-20,20))
    BATD D 8 A_NoBlocking 
	TNT1 A -1 A_BossDeath
    stop
}
}


Actor SCYamatocannon : SCRocketMarauder
{
	damage (400)
	ExplosionDamage 400
	ExplosionRadius 512
	radius 32
	Speed 50
	FastSpeed 60
	DamageType SCYamatocannon
	deathsound "weapons/rocklx"
	scale 1.5
	States
	{
	Spawn:
		BATP A 1 Bright 
		MISL A 0 Bright A_CustomMissile("RocketTrail",Random(4,6),Random(-2,2))
		Loop
	Death:
		MISL B 8 Bright A_Explode
		MISL C 6 Bright
		MISL D 4 Bright 
		Stop
	}
}
Actor SCCruiserLaser : Tracer
{
    scale 2
	+BLOODSPLATTER 
	+THRUGHOST
	-DONTSPLASH
	damage (30)
	+MISSILE
	speed 30
	fastspeed 40
	deathsound "Wraith/ShootE1"
	DamageType SCCruiserLaser
	States
	{
	Spawn:
		LASS B 1 Bright
	Loop
	Death:
		PUFF A 0
		Stop
	}
}