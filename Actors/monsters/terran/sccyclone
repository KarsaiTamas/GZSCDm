Actor StarcraftCyclone:MidTierMech 20021
{

	Health 300
	Radius 20
	Height 56
	Reactiontime 500
	scale 1.3
	meleerange 200
	DropItem "MediumHealth"
	DamageFactor "SCCycloneRocket",0
	mass 500
	MissileHeight 32
	Obituary "%o was killed by a cyclone"
	PainChance 5
	Speed 3
	Species "sccyclone"	
	MinMissileChance 100
	+DONTHARMCLASS
	SeeSound "Cyclone/Allerts"
	DeathSound "Banshee/Death"

	States
	{
	Spawn:
	CYCI A 0
	CYCI A 0 A_JumpIfInventory("Spawn2Token",1,"Spawn2")
	CYCI A 0 A_giveInventory("Spawn2Token",1)
	CYCI A 0 ACS_NamedExecute("TakeHealTokenOnDamagedHP",0)
	Goto Spawn2
	
	Spawn2:
	CYCI A 8 A_Look		
	Loop	

	See:
	CYCM AB 1 A_Chase("RocketShoot","RocketShoot")
	Loop

	Pain:
	CYCM AB 1 A_Wander
	goto See
	
	Pain.PlayerHook:
	CYCI A 0
	CYCI A 64
	Goto See
	
	RocketShoot:
	CYCS A 8
	CYCS A 0 A_PlaySound("Cyclone/ShootS") 
	CYCS A 4 A_FaceTarget
	CYCS B 4 Bright A_CustomMissile("SCCycloneRocket",50,CallACS("ChoseMissleHorizontal",-20,20,Random(1,2)),0)
	CYCS A 32 A_jump(100,"RocketShootHoming")
	Goto See
	
	
	RocketShootHoming:
	CYCS A 1 A_PlaySound("Cyclone/Lock") 
	CYCS AAAAAA 2 A_FaceTarget
	CYCS A 0 A_PlaySound("Cyclone/ShootS") 
	CYCS A 4 A_FaceTarget
	CYCS B 4 Bright A_CustomMissile("SCCycloneRocketHoming",50,CallACS("ChoseMissleHorizontal",-20,20,Random(1,2)),0)
	CYCS A 32 
	Goto See
	
	Death:
	CYCD A 0 A_SCREAM
	SCMI B 0 A_giveInventory("NeedHealingToken",1)
	CYCD ABCDE 4 A_CustomMissile("DeathExplosions",random(40,60),Random(-30,30)) 
	CYCD E 0 A_NoBlocking
	CYCD F -1    
	stop
	}
}


Actor SCCycloneRocket : SCRocketMarauder
{
	damage (40)
	ExplosionDamage 10
	ExplosionRadius 32
	Speed 20
	FastSpeed 30
	DamageType SCCycloneRocket
	deathsound "Cyclone/ShootE"
	scale 0.7
	States
	{
	Spawn:
		CYCP A 1 Bright 
		MISL A 0 Bright A_CustomMissile("RocketTrail",Random(4,6),Random(-2,2))
		Loop
	Death:
		MISL B 8 Bright A_Explode
		MISL C 6 Bright
		MISL D 4 Bright 
		Stop
	}
}


Actor SCCycloneRocketHoming : SCCycloneRocket
{
	+SEEKERMISSILE
	Speed 15
	FastSpeed 25

	States
	{
	Spawn:
		CYCP A 1 Bright A_SeekerMissile(0,5,0,255)
		MISL A 0 Bright A_CustomMissile("RocketTrail",Random(4,6),Random(-2,2))
		Loop
	Death:
		MISL B 8 Bright A_Explode
		MISL C 6 Bright
		MISL D 4 Bright 
		Stop
	}
}
