Actor StarcraftWarbot:HighTierMech 20028
{

	Health 1000
	Radius 24
	Height 64
	scale 1.3
	Reactiontime 500
	meleerange 200
	DropItem "BigHealth"
	DamageFactor "SCWarbotRocket",0
	mass 800
	MissileHeight 32
	Obituary "%o was killed by a warbot"
	PainChance 5
	Speed 3
	Species "scwarbot"	
	MinMissileChance 100
	+DONTHARMCLASS
	SeeSound "Warbot/Allerts"
	DeathSound "Banshee/Death"

	States
	{
	Spawn:
	WRTI A 0 A_JumpIfInventory("Spawn2Token",1,"Spawn2")
	WRTI A 0 A_giveInventory("Spawn2Token",1)
	WRTI A 0 ACS_NamedExecute("TakeHealTokenOnDamagedHP",0)
	Goto Spawn2
	
	Spawn2:
	WRTI A 8 A_Look		
	Loop	

	See:
	WRTI A 1
	WRTM AB 1 A_Chase("RocketShoot","RocketShoot")
	Loop

	Pain:
	WRTM AB 1 A_Wander
	goto See
	
	Pain.PlayerHook:
	WRTI A 0
	WRTI A 64
	Goto See
	
	RocketShoot:
	WRTS A 0 A_PlaySound("Warbot/ShootS",5) 
	WRTS A 4 A_FaceTarget
	WRTS B 4 Bright A_CustomMissile("SCWarbotRocket",50,20)
	WRTS C 4
	WRTS D 0 A_PlaySound("Warbot/ShootS",6) 
	WRTS A 4 A_FaceTarget
	WRTS D 4 Bright A_CustomMissile("SCWarbotRocket",50,-20)
	WRTS EFG 4
	WRTS G 4 
	Goto See
	
	Death:
	WRTD A 0 A_SCREAM
	SCMI B 0 A_giveInventory("NeedHealingToken",1)
	WRTD ABCDEF 4 A_CustomMissile("DeathExplosions",random(40,60),Random(-30,30)) 
	WRTD E 0 A_NoBlocking
	WRTD F -1    
	stop
	}
}


Actor SCWarbotRocket : SCRocketMarauder
{
	damage (20)
	ExplosionDamage 40
	ExplosionRadius 96
	Speed 20
	FastSpeed 30
	RenderStyle "Translucent"
	+HITTARGET	
	DamageType SCWarbotRocket
	deathsound "Warbot/ShootE"
	scale 0.7
	States
	{
	Spawn:
		CYCP A 1 Bright 
		MISL A 0 Bright A_CustomMissile("RocketTrail",Random(4,6),Random(-2,2))
		Loop
	Death:
		MISL B 8 Bright A_Explode
		MISL C 6 Bright
		MISL D 4 Bright 
		Stop
	}
}

